buildscript {
  repositories {
    mavenLocal()
    if (System.getenv('GG_REPO')?.trim()) maven { url System.getenv('GG_REPO') }
    mavenCentral()
  }
  dependencies {
    classpath "kz.greetgo.plugins:greetgo.gradle.plugins:0.0.2"
  }
}

import kz.greetgo.gradle.plugins.ShortJavaPathPlugin

def nowDate = new Date()
def nowFormattedDate = nowDate.format("yyyy-MM-dd/HH:mm:ss")
def vendorName = "greetgo!"

allprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'eclipse'
  apply plugin: 'maven'
  apply plugin: 'signing'

  version = '2.0.2'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  group = "kz.greetgo"

  eclipse.project.natures 'org.springsource.ide.eclipse.gradle.core.nature'
}

subprojects {
  apply plugin: ShortJavaPathPlugin

  repositories {
    mavenLocal()
    if (System.getenv('GG_REPO')?.trim()) maven { url System.getenv('GG_REPO') }
    mavenCentral()
  }

  dependencies {
    ext.greetgoConfVersion = '1.0.1'
    ext.greetgoUtilVersion = '0.1.0'

    compile 'com.google.code.findbugs:annotations:2.0.1'

    testCompile 'org.testng:testng:6.5.1'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
  }

  jar {
    manifest {
      attributes 'Implementation-Title': project.name
      attributes 'Implementation-Version': version
      attributes 'Implementation-Vendor': vendorName
      attributes 'Created-By': System.getProperty("user.name")
      attributes 'Created-At': nowFormattedDate
    }
  }

  test { useTestNG() }
}
